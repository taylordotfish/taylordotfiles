include(esyscmd(`printf "\`%s'" "$HOME"')`/.conf.m4')dnl
merge_env(`HIDPI', `MONOCHROME')dnl
sinclude(`.Xresources.pre.m4')dnl
dnl
Xft.dpi: ifdefn(`HIDPI', `192', `96')

xterm*background: black
xterm*foreground: white
xterm*color4: rgb:0f/6b/b0
xterm*color12: rgb:60/a4/ff
xterm*metaSendsEscape: true
xterm*locale: true
dnl
define_default(`RXVT_COLORS', `dnl
$1.foreground: white
$1.background: [75]#000000
')dnl
dnl
define_default(`RXVT_COLORS_MONOCHROME', `dnl
$1.foreground: black
$1.background: #ffffff
')dnl

RXVT_COLORS(Rxvt-color)
ifdefn(`MONOCHROME', `pushdef(`RXVT_COLORS', `RXVT_COLORS_MONOCHROME($@)')')dnl
RXVT_COLORS(Rxvt)dnl

Rxvt.scrollBar: false
Rxvt.color4: #0f6bb0
Rxvt.color12: #60a4ff
Rxvt.depth: 32
dnl
define_default(`RXVT_FONTS', `dnl
$1.font: -misc-fixed-medium-r-normal--15-140-75-75-c-90-iso10646-1
$1.boldFont: -misc-fixed-bold-r-normal--15-140-75-75-c-90-iso10646-1
$1.italicFont: -misc-fixed-bold-r-normal--15-140-75-75-c-90-iso10646-1
$1.boldItalicFont: -misc-fixed-bold-r-normal--15-140-75-75-c-90-iso10646-1
')dnl
dnl
define_default(`RXVT_FONTS_HIDPI', `dnl
$1.font: -misc-2xfixed-medium-r-normal--30-280-75-75-c-180-iso10646-1
$1.boldFont: -misc-2xfixed-bold-r-normal--30-280-75-75-c-180-iso10646-1
$1.italicFont: -misc-2xfixed-bold-r-normal--30-280-75-75-c-180-iso10646-1
$1.boldItalicFont: -misc-2xfixed-bold-r-normal--30-280-75-75-c-180-iso10646-1
')dnl
dnl
define_default(`RXVT_FONTS_MONOCHROME', `dnl
$1.font: xft:DejaVu Sans Mono:pixelsize=22
')dnl

ifdefn(`HIDPI', `pushdef(`RXVT_FONTS', `RXVT_FONTS_HIDPI($@)')')dnl
RXVT_FONTS(Rxvt-color)
ifdefn(`MONOCHROME', `pushdef(`RXVT_FONTS', `RXVT_FONTS_MONOCHROME($@)')')dnl
RXVT_FONTS(Rxvt)dnl

sinclude(`.Xresources.post.m4')dnl
! `vim:ft=xdefaults'
dnl vim:ft=m4
